locals {
  public_subnets = {
    subnet1 = {
      name              = var.subnet1_name
      cidr_block        = var.subnet1_cidr
      availability_zone = var.availability_zone1
    }
    subnet2 = {
      name              = var.subnet2_name
      cidr_block        = var.subnet2_cidr
      availability_zone = var.availability_zone2
    }
    subnet3 = {
      name              = var.subnet3_name
      cidr_block        = var.subnet3_cidr
      availability_zone = var.availability_zone3
    }
  }
}

resource "aws_vpc" "main" {
  cidr_block           = var.vpc_cidr
  enable_dns_hostnames = true
  enable_dns_support   = true

  tags = {
    Name = var.vpc_name
  }
}

resource "aws_subnet" "public" {
  for_each = local.public_subnets

  vpc_id                  = aws_vpc.main.id
  cidr_block              = each.value.cidr_block
  availability_zone       = each.value.availability_zone
  map_public_ip_on_launch = true

  tags = {
    Name = each.value.name
  }
}

resource "aws_internet_gateway" "main" {
  vpc_id = aws_vpc.main.id

  tags = {
    Name = var.internet_gateway_name
  }
}

resource "aws_route_table" "public" {
  vpc_id = aws_vpc.main.id

  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.main.id
  }

  tags = {
    Name = var.routing_table_name
  }
}

resource "aws_route_table_association" "public" {
  for_each = aws_subnet.public

  subnet_id      = each.value.id
  route_table_id = aws_route_table.public.id
}
